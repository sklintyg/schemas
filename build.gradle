plugins {
    id "se.inera.intyg.plugin.common" version "3.1.3" apply false
    id "no.nils.wsdl2java" version "0.10" apply false
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'no.nils.wsdl2java'
    apply plugin: 'maven'

    ext {
        guavaVersion = "20.0"
        jacksonVersion = "2.9.7"
        logbackVersion = "1.0.11"
        // Errorprone has no mechanism (yet) for excluding generated code. We hence only use it for 'schemas-support'.
        errorproneExclude = ".*-schemas\$"
    }

    if (project.hasProperty("codeQuality")) {
        license {
            exclude "**/*.java" // Exclude all.
        }
    }

    repositories {
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        mavenCentral {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    dependencies {
        def jaxb2Version = "1.11.1"
        def cxfVersion = "3.2.4"

        compile "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
        compile "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"
        compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxb2Version}"
        compile "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3"
        compile "javax.xml.bind:jaxb-api:2.3.1"
        compile "javax.xml.ws:jaxws-api:2.3.1"
        compile "org.glassfish.jaxb:jaxb-runtime:2.3.2"
        compile "org.glassfish.main.javaee-api:javax.jws:3.1.2.2"
        compile "com.sun.xml.messaging.saaj:saaj-impl:1.5.1"


        testCompile "junit:junit:4.+"

        wsdl2java "javax.xml.bind:jaxb-api:2.3.1"
        wsdl2java "javax.xml.ws:jaxws-api:2.3.1"
        wsdl2java "org.glassfish.jaxb:jaxb-runtime:2.3.2"
        wsdl2java "org.glassfish.main.javaee-api:javax.jws:3.1.2.2"
        wsdl2java "com.sun.xml.messaging.saaj:saaj-impl:1.5.1"
        wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxb2Version}"
        wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics:${jaxb2Version}"
        wsdl2java "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.3"
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // Include schemas in jar file
    sourceSets.main.resources.srcDirs += file("schemas")

    wsdl2java {
        generatedWsdlDir = file("build/generatedSource/java")
        wsdlDir = file("schemas")
    }

    task tagRelease(type: TagReleaseTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://nexus.drift.inera.se/repository/maven-releases/") {
            authentication(userName: System.properties['ineraNexusUsername'], password: System.properties['ineraNexusPassword'])
        }
    }
}

configure(subprojects.findAll { !['support', 'intyg-clinicalprocess-healthcond-certificate-schematron'].contains(it.name) }) {
    dependencies {
        compile "se.inera.intyg.schemas:schemas-support:2.1.5"
    }
}
