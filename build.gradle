ext.nexusUsername = System.properties['nexusUsername']
ext.nexusPassword = System.properties['nexusPassword']

allprojects {
  apply plugin: 'maven'
}

subprojects {
 uploadArchives {
    repositories {
      mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
          authentication(userName: nexusUsername, password: nexusPassword)
        }
      }
    }
  }
}

configure(subprojects.findAll { ! ['intyg-clinicalprocess-healthcond-certificate-schematron'].contains(it.name) }) {
  apply plugin: 'java'
  apply plugin: 'no.nils.wsdl2java'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  // Include schemas in jar file
  sourceSets.main.resources { srcDirs = ["schemas"] }

  wsdl2java {
    generatedWsdlDir = file("build/generatedSource/java")
    wsdlDir = file("schemas")
  }

  repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
  }

  dependencies {
    def jaxbVersion = "0.9.5"

    compile "org.apache.cxf:cxf-rt-frontend-jaxws:3.1.3"
    compile "org.apache.cxf:cxf-rt-transports-http:3.1.3"
    compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxbVersion}"

    testCompile "junit:junit:4.+"

    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:${jaxbVersion}"
    wsdl2java "org.jvnet.jaxb2_commons:jaxb2-basics:${jaxbVersion}"
  }

}



configure(subprojects.findAll { ! ['support', 'intyg-clinicalprocess-healthcond-certificate-schematron'].contains(it.name) }) {
  dependencies {
    compile "se.inera.intyg.schemas:schemas-support:2.1.0"
  }
}

buildscript {
  dependencies {
    classpath 'no.nils:wsdl2java:0.10'
  }

  repositories {
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
}
